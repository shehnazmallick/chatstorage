plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Chat history storage microservice'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'com.bucket4j:bucket4j-core:8.10.1'
    implementation 'com.bucket4j:bucket4j-redis:8.10.1'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.15.2'
    testRuntimeOnly 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(21)
    }
    finalizedBy tasks.named('jacocoTestReport')
}

jacoco {
    toolVersion = '0.8.12'
}

tasks.named('jacocoTestReport') {
    dependsOn tasks.named('test')
    reports {
        xml.required = true
        html.required = true
    }
    classDirectories.setFrom(
            files(sourceSets.main.output).asFileTree.matching {
                exclude(
                        'com/example/chatstorage/ChatstorageApplication.class',
                        'com/example/chatstorage/config/**',
                        'com/example/chatstorage/dto/**',
                        'com/example/chatstorage/entity/**',
                        'com/example/chatstorage/exception/**',
                        'com/example/chatstorage/repository/**',
                        'com/example/chatstorage/security/RedisRateLimiterService.class',
                        'com/example/chatstorage/security/RequestLoggingFilter.class'
                )
            }
    )
}

tasks.named('jacocoTestCoverageVerification') {
    dependsOn tasks.named('test')
    classDirectories.setFrom(tasks.named('jacocoTestReport').get().classDirectories)
    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('jacocoTestCoverageVerification')
}
